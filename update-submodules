#!/usr/bin/env zsh

set -e

function .colorize_summary () {
  builtin emulate -L zsh

  local -a tokens
  local up_to_date=true
  while IFS= read -r; do
    up_to_date=false
    set -A tokens "${(@s: :)REPLY//\%/%%}"
    case $REPLY in
      \**)
        print -P "%6F$tokens[1]%f %2F$tokens[2]%f %6F$tokens[3]%f ${tokens[4, -1]}"
        ;;
      $'  >'*)
        print -P "  %2F${tokens[3]}%f %3F${tokens[4,-1]}%f"
        ;;
      $'  <'*)
        print -P "  %1F${tokens[3]}%f %3F${tokens[4,-1]}%f"
      ;; *)
        print "$REPLY"
      ;;
    esac;
  done
  # NOTE: assumes lack of output means success
  if [[ $up_to_date = true ]]; then
    print -P " %2F***%f All submodules updated %2F***%f"
  fi
}

git submodule update --init --remote --recursive --merge "$@"
git submodule summary | .colorize_summary
